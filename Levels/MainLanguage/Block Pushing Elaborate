title Microban
author David Skinner
homepage sneezingtiger.com/sokoban/levels/microbanText.html

( the first ten levels of microban. I DON'T HAVE PERMISSION FOR THESE PARTICULAR LEVELS, THEY MAY NOT MAKE IT OUT OF THE BETA )

========
OBJECTS
========

Background 
LIGHTGREEN GREEN
11111
01111
11101
11111     
10111   


Target 
DarkBlue 
..... 
.000.
.0.0.
.000.
.....     

Wall     
BROWN DARKBROWN     
00010
11111
01000
11111
00010

Player 
Black Orange White Blue     
.000.
.111.
22222
.333.
.3.3.

Crate
Orange Yellow
00000
0...0
0...0
0...0
00000


=======
LEGEND
=======

. = Background
# = Wall
P = Player
* = Crate
@ = Crate and Target
O = Target


=======
SOUNDS
=======

Crate MOVE 36772507
endlevel 83744503
startgame 92244503

================
COLLISIONLAYERS
================

Background
Target
Player, Wall, Crate

======
RULES     
======     

[ >  Player | Crate ] -> [  >  Player | > Crate  ]     

==============
WINCONDITION
==============

All Target on Crate     

=======     
LEVELS
=======

####..
#.O#..
#..###
#@P..#
#..*.#
#..###
####..

######
#....#
#.#P.#
#.*@.#
#.O@.#
#....#
######

..####...
###..####
#.....*.#
#.#..#*.#
#.O.O#P.#
#########

########
#......#
#.O@@*P#
#......#
#####..#
....####

.#######
.#.....#
.#.O*O.#
##.*P*.#
#..O*O.#
#......#
########

######.#####
#....###...#
#.**.....#P#
#.*.#OOO...#
#...########
#####.......

#######
#.....#
#.O*O.#
#.*O*.#
#.O*O.#
#.*O*.#
#..P..#
#######

..######
..#.OOP#
..#.**.#
..##.###
...#.#..
...#.#..
####.#..
#....##.
#.#...#.
#...#.#.
###...#.
..#####.

#####.
#O..##
#P**.#
##...#
.##..#
..##O#
...###

......#####
......#O..#
......#O#.#
#######O#.#
#.P.*.*.*.#
#.#.#.#.###
#.......#..
#########..
