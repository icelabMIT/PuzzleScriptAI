title turnament
author aaron steed
homepage www.nitrome.com

zoomscreen 11x11

========
OBJECTS
========

(temporary objects, for programming)

Temp
black

DangerZone 
black

(game objects)

Background
darkgray

Target 
lightblue
00000
0...0
0...0
0...0
00000

Player P
RED BLACK white
.2.2.
.000.
01110
00100
00100

Wall     
BLUE PURPLE
00010
11111
01000
11111
00010

Vase V
Pink White
00000
.000.
00000
11111
00000

SlimeSpawner W
black

Slime_Primed S
green lightgreen
..1..
.101.
10001
10001
.111.

Slime_Tired
green lightgreen
.....
..1..
11011
.111.
.....

Button_OpenDoor B
blue lightblue darkblue
.....
.000.
.102.
.102.
.....

Button_Down
blue lightblue darkblue
.....
.....
.000.
.102.
.....


Button_CloseDoor C
blue lightblue darkblue
.....
.000.
.102.
.102.
.....


Door D
darkblue
.0.0.
00000
.0.0.
00000
.0.0.

EnemyDoor I
darkblue
.0.0.
00000
.0.0.
00000
.0.0.

Door_Down E
darkblue
.....
.....
00000
.....
.....

EnemyDoor_Down
darkblue
.....
.....
00000
.....
.....

Fire0 F
black red yellow
.....
..0..
.0.0.
..0..
.....

Fire1 G
black red yellow
.....
..1..
.1.1.
..1..
.....


Fire2 H
black red yellow
1.2.1
.222.
.121.
..1..
1...1


Skull K
white gray black
.000.
02020
10001
.101.
.000.

SkullSpawner U
black

=======
LEGEND
=======
Button = Button_OpenDoor or Button_CloseDoor
Slime = Slime_Primed or Slime_Tired
Enemy = Slime or Vase or Skull
LivingEnemy = Slime or Skull
Entity = Vase or Player or Slime or Temp or Skull
Fire = Fire0 or Fire1 or Fire2
Spawner = SkullSpawner or SlimeSpawner
. = Background
# = Wall
T = Target

=========
SOUNDS
=========

================
COLLISIONLAYERS
================

Spawner
Temp, DangerZone
Background
Target, Door_Down, Button, Button_Down,Fire, EnemyDoor_Down
Wall, Entity, Door, EnemyDoor

======
RULES     
======     

[ > Player | Enemy ] -> [ Player | ]

[ > Player | ] -> [ > Player | DangerZone ]


(Skull kills player)
[ Skull | Dangerzone ] [ Player ] -> [ > Skull | ] [ ]
[ Skull ] -> [ randomDir Skull ]


(Slime Kills Player)
[ Slime_Primed | DangerZone ] [ Player ] -> [  | Slime_Tired ] [ ]

[ Slime_Tired ] -> [ Temp ]
[ Slime_Primed  ] -> [ randomDir Slime_Tired ]
[ Temp ] -> [ Slime_Primed ]

[ DangerZone ] -> []

(BUTTON LOGIC)

(open/close doors)
late [ Player Button_OpenDoor |...| Door ] -> [  Player Button_OpenDoor |...| Door_Down ]
late [ Player Button_CloseDoor |...| Door_Down ] -> [  Player Button_CloseDoor |...| Door ]
late [ Player Button_CloseDoor |...| EnemyDoor_Down ] -> [  Player Button_CloseDoor |...| EnemyDoor ]

(spawn enemies)
late [ Player Button |...| SlimeSpawner ] -> [ Player Button |...| Slime_Primed ]
late [ Player Button |...| SkullSpawner ] -> [ Player Button |...| Skull ]

(finally, hit the button)
late [ Player Button ] -> [ Player Button_Down ]

(FIRE LOGIC)

[Fire2] -> [temp]
[Fire1] -> [Fire2]
[Fire0] -> [Fire1]
[temp] -> [fire0]

late [ Player Fire2 ] -> [ Fire2 ]

(door that disappears if no enemies)
late [ EnemyDoor ] -> [temp]
late [ LivingEnemy ] [ Temp ] -> [LivingEnemy] [EnemyDoor]
late [ Temp ] -> [ EnemyDoor_Down ]

==============
WINCONDITION
==============

Some Player on Target

=======     
LEVELS
=======

...................................
..#####....#######.................
..#...######V...V#.................
..#.P.V..........#.................
..#...######V...V#.................
..#####....###.###.................
.............#.#...................
.............#.#...#######.........
............##.###.#..V..#....#####
............#..VV###..V..######...#
............#..VV.....#S........T.#
............#..VV###..V..######...#
............######.#..V..#....#####
...................#######.........


........###.................
........#V#####.............
.......#V.....B#............
........#.############......
.......##.####.#.W...B######
########....v###.....###...#
#P...S.V......D......DD..T.#
########....v###.....###...#
.......#######.#.W....B#####
...............########.....


.....#####...
.....#...#...
.....#.P.#...
.....#...#...
.....##.##...
......#.#....
....###.###..
....#.....#..
....#hhhhh#..
....#.....#..
....#V###V#..
....#.###.#..
....#S.V.S#..
....###.###..
......#.#....
..#####.#####
..#.........#
..#f#f#.#g#f#
..#g#h#.#f#g#
..#h#g#.#h#h#
..#f#f#.#g#f#
..#.B.#D#.#.#
..#####S#.#.#
......#D#.B.#
......#S#####
......#S#....
......#.#....
.....##.#....
.....#.V#....
.....#i###...
.....#...#...
.....###.#...
......#..#...
......#.##...
...####E###..
...#V..C..#..
...#V.....#..
...#V..U..#..
...#V.....#..
...#V.....#..
...#V.....#..
...####i###..
......#.#....
.....##.##...
.....#...#...
.....#.T.#...
.....#...#...
.....#####...
.............
